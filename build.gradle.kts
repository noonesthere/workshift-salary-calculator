import org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_21
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
  alias(libs.plugins.kotlin.jvm)
}

group = "calculator"
version = "1.0-SNAPSHOT"

repositories {
  mavenCentral()
  maven { url = uri("https://jitpack.io") }
}

dependencies {
  implementation(libs.klite.server)
  implementation(libs.klite.jdbc)
  implementation(libs.klite.json)
  implementation(libs.klite.liquibase)
  implementation(libs.hikari)
  implementation(libs.h2)
  implementation(libs.snakeyaml)
  implementation(libs.poi)
  implementation(libs.poi.ooxml)
}

sourceSets {
  main {
    kotlin.srcDirs("src")
    resources.srcDirs("src", "db").exclude("**/*.kt")
  }
  test {
    kotlin.srcDirs("test")
    resources.srcDirs("test").exclude("**/*.kt")
  }
}

tasks.test {
  useJUnitPlatform()
  jvmArgs("-DENV=test", "--add-opens=java.base/java.lang=ALL-UNNAMED", "-XX:-OmitStackTraceInFastThrow")
}

tasks.withType<KotlinCompile> {
  compilerOptions {
    jvmTarget = JVM_21
  }
  if (System.getProperty("user.name") != "root") finalizedBy("types.ts")
}

tasks.register<Copy>("deps") {
  description = "helpers"
  group = "helpers"
  into("$buildDir/libs/deps")
  from(configurations.runtimeClasspath)
}

val mainClassName = "calculator.LauncherKt"

tasks.jar {
  dependsOn("deps")
  doFirst {
    manifest {
      attributes(
        mapOf(
        "Main-Class" to mainClassName,
        "Class-Path" to File("$buildDir/libs/deps").listFiles()?.joinToString(" ") { "deps/${it.name}" }
      ))
    }
  }
}

tasks.register<JavaExec>("run") {
  description = "helpers"
  group = "helpers"
  workingDir(rootDir)
  jvmArgs("--add-exports=java.base/sun.net.www=ALL-UNNAMED")
  mainClass.set(mainClassName)
  classpath = sourceSets.main.get().runtimeClasspath
}

tasks.register<JavaExec>("types.ts") {
  description = "helpers"
  group = "helpers"
  dependsOn("classes")
  mainClass.set("klite.json.TSGenerator")
  classpath = sourceSets.test.get().runtimeClasspath
  args(
    "${project.buildDir}/classes/kotlin/main",
    "-o", project.file("ui/src/api/types.ts"),
    "-p", """
      // Generated by ./gradlew types.ts
      // TODO export type Id<T extends Entity<T>> = number & {_of?: T}
      export type Entity<T extends Entity<T>> = {id: Id<T>}
    """.trimIndent() + "\n",
    "java.time.DayOfWeek",
    "-t"
  )
}
